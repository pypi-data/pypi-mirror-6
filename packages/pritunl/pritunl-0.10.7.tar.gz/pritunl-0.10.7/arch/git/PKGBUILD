# Maintainer: Zachary Huff <zach.huff.386@gmail.com>

pkgname=pritunl-git
_pkgname=pritunl
pkgver=0.0.0
pkgrel=1
pkgdesc="Simple openvpn server (git version)"
arch=("any")
license=("AGPL3")
url="https://github.com/${_pkgname}/${_pkgname}"
depends=(
    "python"
    "python2"
    "python2-flask"
    "python2-cherrypy"
    "net-tools"
    "iproute2"
    "openvpn"
)
makedepends=(
    "git"
    "python2-distribute"
    "python2-flask"
    "python2-cherrypy"
)
provides=("${pkgname}")
conflicts=("${_pkgname}")
install=${_pkgname}.install
source=("git+${url}.git")
sha256sums=("SKIP")
options=("emptydirs")
backup=(
    "etc/${_pkgname}.conf"
    "var/lib/${_pkgname}/${_pkgname}.db"
    "var/log/${_pkgname}.log"
)

pkgver() {
  cd "$srcdir/$_pkgname"
  git log -1 --format=%at
}

build() {
    cd "${srcdir}/${_pkgname}"
    python2 setup.py build
}

package() {
    cd "${srcdir}/${_pkgname}"
    mkdir -p "${pkgdir}/var/lib/${_pkgname}"
    python2 setup.py install --root="${pkgdir}" --prefix=/usr --no-upstart
}
